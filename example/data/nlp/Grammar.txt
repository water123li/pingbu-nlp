#======================
# grammar file example
#----------------------

# load lexicons from file
TVChannelName = load("TVChannel.txt");
VideoName = load("Video.txt");
VideoKeyword = load("Video.txt", true);
Artist = load("Artist.txt");
Type = load("Type.txt");
Area = load("Area.txt");
City = load("City.txt");
StockName = load("Stock.txt");

# define common prefix
$Prefix = compile("[麻烦[你]]([请](帮|给|为)我)|我(想|要|想要)");

# define slots about date and time
Year = compile("(<Digit:1800-2099>|前|去|旧|今|明|后)年[度]");
$Month = compile("<Digit:1-12>月[份]|(上上|上|这|下|下下)[个]月|本月");
Month = define("[<Year>[的]]<$Month>");
Week = compile("(上上|上|这|下|下下)([个]星期|周)");
$Date = compile("<Digit:1-31>[日|号]|(前|昨|今|明|后)[日|天|朝]|[(上上|上|这|下|下下)[个]](星期|周)(一|二|三|四|五|六|日|天)");
Date = define("[<Month>[的]]<$Date>");
$DateRange = define("<Year>|<Month>|<Week>|<Date>");
$Time = compile("[<Digit:0-23>(点|时)[整|半|一刻|差一刻|<Digit:1-59>分|多][钟]]");
Time = define("[凌晨|清晨|晨|早|早上|上午|中午|午后|下午|黄昏|傍晚|晚|晚上|夜|夜里|深夜|午夜|半夜|下半夜|后半夜]<$Time>");
$DateTime = define("<Date>|[<Date>[的]]<Time>");

# define grammar for video
Category = compile("影视[剧]|电影|[影]片|[电视]剧|剧集|[电视]节目|动漫");
define("[[<$Prefix>]打开](搜视榜|点播[应用])", "action=video");
Order = compile("热门|热播|好看|好评|最新|最近");
$VideoAction = compile("(查|找|查找|搜[索]|打开|点播|看|播|放|播放)[[一]下]");
define("[[<$Prefix>]<$VideoAction>][[([<Artist>[<Role>]]&[<Order>]&[<Area>]&[<$DateRange>])[的|嘅]][<Type>]<Category>][<VideoName>[节目|<Category>][<Episode>]]", "action=video");
$VideoAction2 = compile("(点播|看|播|放|播放)[[一]下]");
define("[[<$Prefix>]<$VideoAction2>][[([<Artist>[<Role>]]&[<Order>]&[<Area>]&[<$DateRange>])[的|嘅]][<Type>]<Category>][<VideoKeyword>[节目|<Category>][<Episode>]]", "action=video");

# define grammar for TV
$TVChannel = define("<TVChannelName>|<Digit:1-99>[频道|台]|频道<Digit:1-99>|(上|下|前|后)[一]个[频道|台])");
define("[[<$Prefix>]([收]看]|播放)(电视|[电视]直播)|[[<$Prefix>]打开]直播[应用]", "action=tvlive");
define("[[<$Prefix>]([收]看|打开|播放]<TVChannelCategory>[频道]", "action=tvlive");
define("[[<$Prefix>]([收]看|打开|播放)]<$TVChannel>[[的|嘅]节目]", "action=tvlive");
define("[[<$Prefix>](调|跳|转|换|切[换])到]<$TVChannel>", "action=tvlive");
define("[<$Prefix>][打开]回看[应用]", "action=tvback");
define("[<$Prefix>]回看[电视|<$TVChannel>]", "action=tvback");
define("[[<$Prefix>](回看|打开|(调|跳|转|换|切[换])到)][<$TVChannel>]<$DateTime>[[的|嘅]节目]", "action=tvback");

# define grammar for music
Music = define("<MusicName>|<Artist>(的|嘅)(歌[曲]|音乐)[<MusicName>]|<MusicType>");
define("[[<$Prefix>](听|播|放|播放|打开)](歌[曲]|音乐|<Music>)", "action=music");
define("[<$Prefix>](唱K|([演]唱|K)(歌[曲]|<Music>))", "action=ktv");

# define grammar for weather
$QueryAction = compile("(查|查询|查看|搜[索]|看|睇|问[你])[[一]下]|查查看|看看|问问看|问问你");
$CityDate = define("<City>|<Date>|<City><Date>|<Date><City>");
define("[[<$Prefix>]<$QueryAction>][<$CityDate>[的|嘅]]天气[情况|信息|怎[么]样|[还]好吗|好不好|可好]", "action=weather");
$WeatherRainVerb = define("下|降|落");
$WeatherRainVerbQuery = define("下不下|降不降|落不落");
$WeatherRainType = define("毛毛[细]|小|大|中|暴|雷|阵|雷阵");
define("[[<$Prefix>]<$QueryAction>][<$CityDate>][[的|嘅]天[气]][[会]<$WeatherRainVerb>[<$WeatherRainType>]雨吗|会不会<$WeatherRainVerb>[<$WeatherRainType>]雨[呢]|<$WeatherRainVerbQuery>[<$WeatherRainType>]雨[呢]]",
       "action=weather.rain");
$WeatherWindVerb = define("起|刮|吹|来");
$WeatherWindVerbQuery = define("起不起|刮不刮|吹不吹|来不来");
$WeatherWindType = define("阵|微|大|中|暴|台|飓|龙卷");
define("[[<$Prefix>]<$QueryAction>][<$CityDate>][[的|嘅]天[气]][[会]<$WeatherWindVerb>风吗|会不会<$WeatherWindVerb>风[呢]|<$WeatherWindVerbQuery>风[呢]]",
       "action=weather.wind");

# define grammar for stock
Stock = define("[股票|证券]<StockName>|<StockName>[[的|嘅](股票|证券)]");
$StockTimeline = define("分时", "chart=time");
StockKlinePeriod = define("(1|5|15|30|60)分钟|日|周|月|年");
$StockKline = define("[<StockKlinePeriod>]K线", "chart=k");
$StockChart = define("(<$StockTimeline>|<$StockKline>)[图]");
define("[[<$Prefix>](打开|查看)](股市|股票|沪市|上交所|深市|深交所|创业板)[大盘]", "action=stock");
define("[[<$Prefix>](打开|查看)]<Stock>[[的|嘅][股价|价格|行情|公司|<$StockChart>][情况|信息]]", "action=stock");

# define grammar for others
define("[<$Prefix>][打开]翻译", "action=translate");
define("[<$Prefix>](网购|[上网](购物|买东西|买买买))", "action=merchandise");
define("[[<$Prefix>]交][手机|[电]话|水|电|[煤|液化|天然]气|[宽带]([上]网|网络)|[有线]电视|收视]费", "action=charge");
define("[<$Prefix>](上网[冲浪]|(打开|浏览|看)网页)", "action=website");

# define grammar for play control
define("[[<$Prefix>](打开|[查]看)]播放记录", "action=播放记录");
define("[[<$Prefix>](打开|(跳|翻|切)到)](第<Digit:1-200>|((上|下|前|后|第|最(前|后|末))[一])页)", "action=page");
